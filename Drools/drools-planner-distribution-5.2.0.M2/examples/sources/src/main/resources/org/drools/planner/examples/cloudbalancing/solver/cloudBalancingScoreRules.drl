/*
 * Copyright 2010 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.drools.planner.examples.cloudbalancing.solver;
    dialect "java"

import org.drools.planner.core.score.calculator.HardAndSoftConstraintScoreCalculator;
import org.drools.planner.core.score.constraint.IntConstraintOccurrence;
import org.drools.planner.core.score.constraint.ConstraintType;

import org.drools.planner.examples.cloudbalancing.domain.CloudAssignment;
import org.drools.planner.examples.cloudbalancing.domain.CloudBalance;
import org.drools.planner.examples.cloudbalancing.domain.CloudComputer;
import org.drools.planner.examples.cloudbalancing.domain.CloudProcess;

global HardAndSoftConstraintScoreCalculator scoreCalculator;

// ############################################################################
// Hard constraints
// ############################################################################

rule "minimalCpuPowerTotal"
    when
        $cloudComputer : CloudComputer($cpuPower : cpuPower)
        $minimalCpuPowerTotal : Number(intValue > $cpuPower) from accumulate(
            CloudAssignment(
                cloudComputer == $cloudComputer,
                $minimalCpuPower : minimalCpuPower),
            sum($minimalCpuPower)
        )
    then
        insertLogical(new IntConstraintOccurrence("minimalCpuPowerTotal", ConstraintType.NEGATIVE_HARD,
                $minimalCpuPowerTotal.intValue() - $cpuPower,
                $cloudComputer));
end

rule "minimalMemoryTotal"
    when
        $cloudComputer : CloudComputer($memory : memory)
        $minimalMemoryTotal : Number(intValue > $memory) from accumulate(
            CloudAssignment(
                cloudComputer == $cloudComputer,
                $minimalMemory : minimalMemory),
            sum($minimalMemory)
        )
    then
        insertLogical(new IntConstraintOccurrence("minimalMemoryTotal", ConstraintType.NEGATIVE_HARD,
                $minimalMemoryTotal.intValue() - $memory,
                $cloudComputer));
end

rule "minimalNetworkBandwidthTotal"
    when
        $cloudComputer : CloudComputer($networkBandwidth : networkBandwidth)
        $minimalNetworkBandwidthTotal : Number(intValue > $networkBandwidth) from accumulate(
            CloudAssignment(
                cloudComputer == $cloudComputer,
                $minimalNetworkBandwidth : minimalNetworkBandwidth),
            sum($minimalNetworkBandwidth)
        )
    then
        insertLogical(new IntConstraintOccurrence("minimalNetworkBandwidthTotal", ConstraintType.NEGATIVE_HARD,
                $minimalNetworkBandwidthTotal.intValue() - $networkBandwidth,
                $cloudComputer));
end

// ############################################################################
// Soft constraints
// ############################################################################

rule "cloudComputerCost"
    when
        $cloudComputer : CloudComputer($cost : cost)
        exists CloudAssignment(cloudComputer == $cloudComputer)
    then
        insertLogical(new IntConstraintOccurrence("cloudComputerCost", ConstraintType.NEGATIVE_SOFT,
                $cost,
                $cloudComputer));
end

// ############################################################################
// Calculate score
// ############################################################################

// Accumulate hard constraints
rule "hardConstraintsBroken"
        salience -1 // Do the other rules first (optional, for performance)
    when
        $hardTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_HARD, $weight : weight),
            sum($weight) // Vote for http://jira.jboss.com/jira/browse/JBRULES-1075
        )
    then
        scoreCalculator.setHardConstraintsBroken($hardTotal.intValue());
end

// Accumulate soft constraints
rule "softConstraintsBroken"
        salience -1 // Do the other rules first (optional, for performance)
    when
        $softTotal : Number() from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_SOFT, $weight : weight),
            sum($weight) // Vote for http://jira.jboss.com/jira/browse/JBRULES-1075
        )
    then
        scoreCalculator.setSoftConstraintsBroken($softTotal.intValue());
end
